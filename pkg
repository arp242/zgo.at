#!/bin/zsh

set -euC

typeset -A libs=(
	acidtab    ~/code/Golib
	blackmail  ~/code/Golib
	errors     ~/code/Golib
	follow     ~/code/Golib
	gadget     ~/code/Golib
	guru       ~/code/Golib
	hubhub     ~/code/Golib
	isbot      ~/code/Golib
	json       ~/code/Golib
	ps         ~/code/Golib
	sconfig    ~/code/Golib
	termfo     ~/code/Golib
	termtext   ~/code/Golib
	tz         ~/code/Golib
	z18n       ~/code/Golib
	zcache     ~/code/Golib
	zdb        ~/code/Golib
	zhttp      ~/code/Golib
	zli        ~/code/Golib
	zlog       ~/code/Golib
	zprof      ~/code/Golib
	zstd       ~/code/Golib
	zstripe    ~/code/Golib
	ztpl       ~/code/Golib
	zvalidate  ~/code/Golib
)

typeset -A progs=(
	border       ~/code/Prog
	cantuse      ~/code/Prog
	colorcount   ~/code/Prog
	goatcounter  ~/code
	goimport     ~/code/Prog
	gomodgraph   ~/code/Prog
	gosodoff     ~/code/Prog
	har          ~/code/Prog
	singlepage   ~/code/Prog
	sqlbench     ~/code/Prog
	uni			 ~/code/Prog
	zcert        ~/code/Prog
)
#zsrv         ~/code/Prog
# autofox
# orgstat

if [[ ! -f .cache/repos.json ]]; then
	for i in {1..3}; do
		curl -sH 'Accept: application/vnd.github.v3+json' \
			"https://api.github.com/users/arp242/repos?per_page=100&page=$i" \
			>>|.cache/repos.json
	done
fi

rm-v() {
	print ${1%/v?}
}

mk() {
	local name=${1}
	local dir=$2

	local pkg=$(cd $dir && go list)
	local repo=$(cd $dir && git remote get-url origin)
	local repo="https://${${${repo##git@}%%.git}//://}"

	local org=${repo:h:t}
	if [[ $org != arp242 && $org != zgoat ]]; then
		print >&2 "unexpected organisation '$org' in '$repo'; skipping"
		continue
	fi

	printf "%-38s  │  %-26s  │  %s\n" "$(printf '%s (%s)' $pkg $name)" ${dir//$HOME\/code/\~c} $repo

	for p in $(cd "$dir" && go list ./...); do
		name=${p##zgo.at/}
		name=${name##arp242.net/}
		#print "     write $name.html"

		mkdir -p "$name"
		cat <<-EOF | tee "$name.html" "$name/index.html">/dev/null
			<!DOCTYPE html>
			<html><head>
				<meta http-equiv="Content-Type" content="text/html; charset=utf-8">
				<meta name="go-import" content="$pkg git $repo.git">
				<meta name="go-source" content="$pkg $repo $repo/tree/master/{/dir}/ $repo/tree/master/{/dir}/{file}#L{line}">
				<meta http-equiv="refresh" content="0; url=$repo">
			</head><body><p>
				This is the <code>$pkg</code> Go package. Its homepage is at <a href="$repo">$repo</a>.
			</p></body></html>
		EOF
	done
}

index-progs() {
	print '<section class="page">\n<h2>Applications</h2>'
	for name in ${(ok)progs}; do
		local dir=$progs[${name%/v2}]/${name%/v2}
		local pkg=$(cd $dir && go list)
		local repo=$(cd $dir && git remote get-url origin)
		repo="https://${${${repo##git@}%%.git}//://}"

		local desc=$(<.cache/repos.json jq -Mr '.[] | select(.name=="'$name'") | .description')
		desc=${desc%.}.
		desc=${desc//#Go (package|library)( to)?/}
		desc=${desc//% (for|in) Go./.}
		desc=${(U)desc[1]}${desc[2,-1]}
		[[ $desc = "Null." ]] && desc=

		#local org=${repo:h:t}

		<<-EOF
			<div>
				<h3><a href="$repo">$(rm-v $name)</a></h3>
				<code><a href='https://godocs.io/$pkg'>godoc</a></code>
				<p>$desc</p>
			</div>
		EOF
	done
	print '</section>'
}

index-libs() {
	print '<section class="page">\n<h2>Libraries</h2>'
	for name in ${(ok)libs}; do
		local dir=$libs[${name%/v2}]/${name%/v2}
		local pkg=$(cd $dir && go list)
		local repo=$(cd $dir && git remote get-url origin)
		repo="https://${${${repo##git@}%%.git}//://}"

		local desc=$(<.cache/repos.json jq -Mr '.[] | select(.name=="'$name'") | .description')
		desc=${desc%.}.
		desc=${desc//#Go (package|library)( to)?/}
		desc=${desc//% (for|in) Go./.}
		desc=${(U)desc[1]}${desc[2,-1]}
		[[ $desc = "Null." ]] && desc=

		#local org=${repo:h:t}

		<<-EOF
			<div>
				<h3><a href="$repo">$(rm-v $pkg)</a></h3>
				<code><a href='https://godocs.io/$pkg'>godoc</a></code>
				<p>$desc</p>
			</div>
		EOF
	done
	print '</section>'
}

index() {
	awk '/^XXXXX$/ { exit }; { print $0 }' index.tpl
	index-progs
	index-libs
	awk '/^XXXXX$/ { found=1; next }; { if (found) print $0 }' index.tpl
}

# TODO: v3, v4, ...
for name in ${(ok)libs};  mk $name $libs[${name%/v2}]/${name%/v2}
for name in ${(ok)progs}; mk $name $progs[${name%/v2}]/${name%/v2}
index >| index.html
