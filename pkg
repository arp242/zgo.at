#!/bin/zsh

set -euC

typeset -A pkgs=(
	acidtab          ~/code/Golib
	blackmail        ~/code/Golib
	errors           ~/code/Golib
	follow           ~/code/Golib
	gadget           ~/code/Golib
	guru             ~/code/Golib
	hubhub           ~/code/Golib
	isbot            ~/code/Golib
	json             ~/code/Golib
	ps               ~/code/Golib
	sconfig          ~/code/Golib
	termfo           ~/code/Golib
	termtext         ~/code/Golib
	tz               ~/code/Golib
	z18n             ~/code/Golib
	zcache           ~/code/Golib
	zdb              ~/code/Golib
	zhttp            ~/code/Golib
	zli              ~/code/Golib
	zlog             ~/code/Golib
	zprof            ~/code/Golib
	zstd             ~/code/Golib
	zstripe          ~/code/Golib
	ztpl             ~/code/Golib
	zvalidate        ~/code/Golib

	border           ~/code/Prog
	colorcount       ~/code/Prog
	gomodgraph       ~/code/Prog
	har              ~/code/Prog
	singlepage       ~/code/Prog
	zcert            ~/code/Prog
	zsrv             ~/code/Prog

	# autofox
	# cantuse
	# goimport
	# gosodoff
	# orgstat
	# uni uni/v2

	goatcounter      ~/code
)

for name in ${(ok)pkgs}; do
	dir=$pkgs[${name%/v2}]/${name%/v2}  # TODO: v3, v4, vn...
	pkg=$(cd $dir && go list)
	repo=$(cd $dir && git remote get-url origin)
	repo="https://${${${repo##git@}%%.git}//://}"

	org=${repo:h:t}
	if [[ $org != arp242 && $org != zgoat ]]; then
		print >&2 "unexpected organisation '$org' in '$repo'; skipping"
		continue
	fi

	printf "%-38s  │  %-26s  │  %s\n" "$(printf '%s (%s)' $pkg $name)" ${dir//$HOME\/code/\~c} $repo

	for p in $(cd "$dir" && go list ./...); do
		name=${p##zgo.at/}
		name=${name##arp242.net/}
		#print "     write $name.html"

		mkdir -p "$name"
		cat <<-EOF | tee "$name.html" "$name/index.html">/dev/null
			<!DOCTYPE html>
			<html><head>
				<meta http-equiv="Content-Type" content="text/html; charset=utf-8">
				<meta name="go-import" content="$pkg git $repo.git">
				<meta name="go-source" content="$pkg $repo $repo/tree/master/{/dir}/ $repo/tree/master/{/dir}/{file}#L{line}">
				<meta http-equiv="refresh" content="0; url=$repo">
			</head><body><p>
				This is the <code>$pkg</code> Go package. Its homepage is at <a href="$repo">$repo</a>.
			</p></body></html>
		EOF
	done
done
